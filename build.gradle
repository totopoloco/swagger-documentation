plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.openapi.generator' version '7.5.0'
}

group = 'at.mavila.rest'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-integration'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.integration:spring-integration-http'
    implementation 'org.springframework.integration:spring-integration-jpa'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.integration:spring-integration-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //Open API dependencies
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-api:2.5.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    annotationProcessor 'io.swagger.core.v3:swagger-annotations:2.2.21'

}

tasks.named('test') {
    useJUnitPlatform()
}

openApiValidate {
    inputSpec = "$rootDir/src/main/resources/static/swagger.yaml".toString()
}

openApiMeta {
    generatorName = "Sample"
    packageName = "org.openapitools.example"
    outputFolder = "$buildDir/meta".toString()
}


//openApiGenerate {
//    inputSpec = "$projectDir/src/main/resources/static/swagger.yaml".toString()
//    outputDir = "$buildDir/generate-resources".toString()
//    generatorName = "csharp"
//    apiPackage = "at.mavila.rest.documentation.api"
//    modelPackage = "at.mavila.rest.documentation.model"
//    configOptions = [
//            targetFramework : 'net8.0',
//            optionalProjectFile: 'true',
//            hideGenerationTimestamp: 'true',
//            packageName: 'OpenApiNetCoreClient',
//    ]
//}

openApiGenerate {
    inputSpec = "$projectDir/src/main/resources/static/swagger.yaml".toString()
    outputDir = "$buildDir/generate-resources".toString()
    generatorName = "spring"
    apiPackage = "at.mavila.rest.documentation.api"
    modelPackage = "at.mavila.rest.documentation.model"
    configOptions = [
            dateLibrary            : 'java8-localdatetime',
            interfaceOnly          : 'true',
            generateClientAsBean   : 'false',
            useJakartaEe           : 'true',
            useBeanValidation      : 'true',
            skipDefaultInterface   : 'true',
            hideGenerationTimestamp: 'true',
            useSpringBoot3         : 'true',
            useSpringController    : 'true',
            useOptional            : 'true',
            serializableModel      : 'true',
            cleanupOutputDir       : 'true',
            useSwaggerUI           : 'true',
            annotationLibrary      : 'swagger2',

    ]


}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', "$buildDir/generate-resources/src/main/java"]
        }
    }
}

compileJava.dependsOn tasks.named('openApiGenerate')
